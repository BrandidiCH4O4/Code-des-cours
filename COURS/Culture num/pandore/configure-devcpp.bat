@echo off
rem
rem Copyright (c) 2013, GREYC.
rem All rights reserved
rem
rem You may use this file under the terms of the BSD license as follows:
rem
rem "Redistribution and use in source and binary forms, with or without
rem modification, are permitted provided that the following conditions are
rem met:
rem   * Redistributions of source code must retain the above copyright
rem     notice, this list of conditions and the following disclaimer.
rem   * Redistributions in binary form must reproduce the above copyright
rem     notice, this list of conditions and the following disclaimer in
rem     the documentation and/or other materials provided with the
rem     distribution.
rem   * Neither the name of the GREYC, nor the name of its
rem     contributors may be used to endorse or promote products
rem     derived from this software without specific prior written
rem     permission.
rem
rem THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS
rem "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT
rem LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR
rem A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT
rem OWNER OR CONTRIBUTORS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
rem SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT
rem LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE,
rem DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY
rem THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
rem (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
rem OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE."
rem
rem For more information, refer to:
rem https://clouard.users.greyc.fr/Pandore/
rem

rem   @(#)configure-vc2005Express.bat

rem
rem   @(#)configure-devc++.bat

rem 
rem @author Régis Clouard - 2007-12-21
rem 


rem   *** Set the DEV C++ Home 
rem   *** ex set DEVCPPHOME=C:\Program Files\Dev-Cpp
set DEVCPPHOME=C:\Dev-Cpp

rem   *** Set the following variables if needed.
set CPP=g++
set CPPFLAGS=-DNDEBUG -Iinclude -O2 -s -Isrc/operatorsP0
set CPPFLAGSOBJ=$(CPPFLAGS)
set CPPFLAGSOP=$(CPPFLAGS)

set LD=g++
set LDFLAGSLIB=
set LDFLAGSOP=-DMAIN -Llib -lpandore

set AR=ar
set ARFLAGS=rc

set MAKE=make




















cls

rem Pandore directory
set PANDOREHOME=%cd%

set HOME=%HOMEDRIVE%%HOMEPATH%


if not exist ".\src" goto :binary

rem *********** DEVELOPER VERSION
set tarball=developer
echo.
echo Pandore Installation
echo Developer version
echo -------------------------------------
echo.

rem ----------------------------------------------------------------------
rem    Search for Dev C++ directory = Search make and g++

echo searching Dev C++ directory ...

if exist "%DEVCPPHOME%\bin\g++.exe" goto :found

setlocal
for /f "Tokens=*" %%a in ('dir /s /b /ad "%ProgramFiles%\"') do (
 set DEVCPPHOME=%%a
 if exist "%%a\bin\g++.exe" goto :found
)
endlocal

rem ... not found
echo *** ERROR. Cannot find Dev C++ directory in traditional
echo            program files directories
echo     Please edit %0 by hand
echo     and set DEVCPPHOME with the convenient C++ compiler directory;
echo     for example:
echo         set DEVCPPHOME=C:\Program Files\Dev-Cpp
echo.
goto :binary

:found
echo    ... found in %DEVCPPHOME%.

rem ----------------------------------------------------------------------
rem    Add contingent boxes
echo adding boxes ...
for /f %%a in ('dir /b /ad "boxes\"') do (
	echo ... %%a
	xcopy /I /Y /S boxes\%%a\doc doc\operators\%%a
rem	xcopy /I /Y /S boxes\%%a\src src\operatorsP1\%%a
)

rem ----------------------------------------------------------------------
rem    Generate the makefile.bat file.
set makefile_file=./Makefile
echo ## Makefile PANDORE> %makefile_file%
echo ##>> %makefile_file%
echo ##----------------------------------------------------------->> %makefile_file%
echo ## DO NOT EDIT BY HAND>> %makefile_file%
echo ## Autogenerated by ./configure.>> %makefile_file%
echo ## If something is wrong, fix ./configure and run it again.>> %makefile_file%
echo ##----------------------------------------------------------->> %makefile_file%
echo ##>> %makefile_file%
echo ## GREYC - IMAGE >> %makefile_file%
echo ## 6, boulevard Maréchal Juin 14050 CAEN. >> %makefile_file%
echo ## email : Regis.Clouard@greyc.ensicaen.fr>> %makefile_file%
echo ##>> %makefile_file%
echo.>> %makefile_file%
echo CPP=%CPP%>> %makefile_file%
echo CPPFLAGS=%CPPFLAGS%>> %makefile_file%
echo CPPFLAGSOBJ=%CPPFLAGSOBJ%>> %makefile_file%

echo CPPFLAGSOP=%CPPFLAGSOP%>> %makefile_file%
echo LD=%LD%>> %makefile_file%
echo LDFLAGSLIB=%LDFLAGSLIB%>> %makefile_file%
echo LDFLAGSOP=%LDFLAGSOP%>> %makefile_file%
echo.>> %makefile_file%
echo AR=%AR%>> %makefile_file%
echo ARFLAGS=%ARFLAGS%>> %makefile_file%
echo.>> %makefile_file%

echo .PHONY: all clean distclean lib opers>> %makefile_file%
echo .IGNORE:>> %makefile_file%
echo.>> %makefile_file%

echo DEPENDENCIES=\>> %makefile_file%
for /f %%a in ('dir include\*.h /b') do (
	echo include/%%a\>>%makefile_file%
	)
)

echo.>> %makefile_file%
echo OBJS=\>> %makefile_file%
echo 	src/objects/bundled.o\>> %makefile_file%
echo 	src/objects/collection.o\>> %makefile_file%
echo 	src/objects/errc.o\>> %makefile_file%
echo 	src/objects/graph.o\>> %makefile_file%
echo 	src/objects/main.o\>> %makefile_file%
echo 	src/objects/pobject.o\>> %makefile_file%
echo 	src/objects/region.o>> %makefile_file%
echo.>> %makefile_file%

echo adding operators ...
echo OPERS=\>> %makefile_file%
for /f %%d in ('dir src\operatorsP* /b /ad') do (
	for /f %%a in ('dir src\%%d /b /ad') do (
		for /f "tokens=1 delims=." %%b in ('dir src\%%d\%%a\*.cpp /b') do (
			echo 	bin/%%b.exe\>>%makefile_file%
			echo           %%b
		)
	)
)
echo.>> %makefile_file%
echo generating makefile...
echo all: lib opers>> %makefile_file%
echo.>> %makefile_file%
echo lib: lib\pandore.lib>> %makefile_file%
echo.>> %makefile_file%
echo opers: $(OPERS)>> %makefile_file%
echo.>> %makefile_file%
echo lib\pandore.lib: $(OBJS) $(DEPENDENCIES)>> %makefile_file%
echo 	$(AR) $(ARFLAGS) lib\pandore.lib $(OBJS)>> %makefile_file%
echo.>> %makefile_file%
rem ** Objects
echo .cpp.o:>> %makefile_file%
echo 	$(CPP) $(CPPFLAGS) $(CPPFLAGSOBJ) $*.cpp -c -o $@>>%makefile_file%
echo.>> %makefile_file%

rem ** Operators
for /f %%d in ('dir src\operatorsP* /b /ad') do (
	for /f %%a in ('dir src\%%d /b /ad') do (
		echo    - adding targets for %%a
		echo bin/%%.exe:src/operatorsP0/%%a/%%.cpp>> %makefile_file%
		call :content
	)
)
goto :next
:content
	echo 	$(CPP) $(CPPFLAGS) $(CPPFLAGSOP) -o $@ "$<" $(LDFLAGSOP)>> %makefile_file%
	echo.>> %makefile_file%
goto :EOF

:next
echo clean:>> %makefile_file%
echo 	-@rm -f src\objects\\*.o>> %makefile_file%

echo.>> %makefile_file%
echo distclean: clean>> %makefile_file%
echo 	-@rm -f lib\pandore.lib>> %makefile_file%
echo 	-@rm -f Makefile>> %makefile_file%
echo 	-@rm -f pandore.bat>> %makefile_file%
echo 	-@rm -f install.bat>> %makefile_file%
rem for /f %%a in ('dir /b /ad "boxes\"') do (
rem 	echo 	-@rm -f src\operatorsP1\%%a\*>> %makefile_file%
rem 	echo 	-@rm -f doc\operators\%%a\*>> %makefile_file%
rem )
echo 	-@rm -f $(OPERS)>> %makefile_file%

echo.>> %makefile_file%
for /f %%d in ('dir src\operatorsP* /b /ad') do (
	for /f %%a in ('dir src\%%d /b /ad') do (
		echo    - adding dependencies for %%a
		for /f "tokens=1 delims=." %%b in ('dir src\%%d\%%a\*.cpp /b') do (
			echo bin/%%b.exe: src/%%d/%%a/%%b.cpp>>%makefile_file%
		)
	)
)
echo.>> %makefile_file%

rem ----------------------------------------------------------------------
rem    Generate the install.bat file.
set install_file=./install.bat
echo generating install.bat ...
echo @echo off>%install_file%
echo rem>>%install_file%
echo rem   @(#)install.bat               PANDORE>>%install_file%
echo. >>%install_file%
echo rem   Installation script for Pandore>>%install_file%
echo rem   using Dev C++ environment>>%install_file%
echo. >>%install_file%
echo set PATH=%%PATH%%;%DEVCPPHOME%\bin;%%PANDOREHOME%%\bin>>%install_file%
echo echo Compiling Pandore ...>>%install_file%
echo %MAKE% all>>%install_file%
echo rem Installing Pandore ...>>%install_file%
echo rem copy pandore.bat $USERPROFILE\Bureau>>%install_file%
echo echo Successfully installed>>%install_file%
echo echo.>>%install_file%
echo echo Now, launch the script 'pandore' and try>>%install_file%
echo echo the sequence:>>%install_file%
echo echo    pversion>>%install_file%
echo echo    pbmp2pan examples\tangram.bmp examples\tangram.pan>>%install_file%
echo echo    pvisu examples\tangram.pan>>%install_file%
echo pause>>%install_file%

rem ----------------------------------------------------------------------
rem    Generate pandore script
echo generating pandore script ...
rem call "%DEVCPPHOME%\bin\vcvars32.bat"
call :script_prologue
echo. >> .\pandore.bat
echo set PATH=%%PATH%%;%%PANDOREHOME%%\bin>>.\pandore.bat
call :script_epilogue
goto :summary

:binary
rem *******************  BINARY VERSION
set tarball=Binary
echo.
echo Installation of the binary version
echo ----------------------------------
echo.

rem ----------------------------------------------------------------------
rem    Generate pandore script
echo generating pandore script ...
call :script_prologue
echo set PATH=%%PATH%%;%%PANDOREHOME%%\bin>>.\pandore.bat
call :script_epilogue
goto :summary

:script_prologue
rem common file header
echo @echo off> .\pandore.bat
echo set PANDOREHOME=%PANDOREHOME%>> .\pandore.bat
echo set HOME=%%HOMEDRIVE%%%%HOMEPATH%%>> .\pandore.bat
echo set PATH=%%PATH%%;%DEVCPPHOME%\bin>> .\pandore.bat
echo echo PANDORE Version >>.\pandore.bat
echo type "%%PANDOREHOME%%\version.txt">>.\pandore.bat
echo echo GREYC-IMAGE @ Caen France>>.\pandore.bat
echo echo. >>.\pandore.bat
goto :EOF

:script_epilogue
echo. >>.\pandore.bat
echo prompt Pandore$G >>.\pandore.bat
echo if not %%1x==/Cx cmd /T:1F /k>>.\pandore.bat
goto :EOF

:summary
echo.
echo SUMMARY
echo.
echo    Version of Pandore: %tarball%
echo    Local installation of Pandore in: %PANDOREHOME%
echo    User directory is: %HOME%
echo.
if "%tarball%" neq "developer" goto :end
echo    Microsoft Visual Directory is: %DEVCPPHOME%
echo    C++ compiler is: %CPP%.exe
echo    Linker is: %LD%.exe
echo    Make is : %MAKE%.exe
echo.
echo If this is correct, you can just launch the 'install' script.

:end
pause
